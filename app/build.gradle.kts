import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.compose)
    alias(libs.plugins.kotlin.serialization)
    id("org.openapi.generator") version "7.19.0"
}

openApiGenerate {
    generatorName.set("kotlin")
    inputSpec.set("$projectDir/specs/openapi-spec.json")
    outputDir.set(layout.buildDirectory.dir("generate-resources/openapi").map { it.asFile.absolutePath })
    apiPackage.set("com.brokechef.recipesharingapp.api")
    modelPackage.set("com.brokechef.recipesharingapp.data.models.openapi")
    configOptions.set(
        mapOf(
            "library" to "jvm-ktor",
            "serializationLibrary" to "kotlinx_serialization",
        ),
    )
}

val bundleId = "com.brokechef.recipesharingapp"

android {
    namespace = bundleId
    compileSdk = 36

    defaultConfig {
        applicationId = bundleId
        minSdk = 24
        targetSdk = 36
        versionCode = 1
        versionName = "1.0"

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            isMinifyEnabled = false
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro",
            )
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }
    buildFeatures {
        compose = true
    }
}

dependencies {
    implementation(libs.androidx.core.ktx)
    implementation(libs.androidx.lifecycle.runtime.ktx)
    implementation(libs.androidx.lifecycle.viewmodel.compose)
    implementation(libs.androidx.activity.compose)
    implementation(platform(libs.androidx.compose.bom))
    implementation(libs.androidx.compose.ui)
    implementation(libs.androidx.compose.ui.graphics)
    implementation(libs.androidx.compose.ui.tooling.preview)
    implementation(libs.androidx.compose.material3)
    implementation(libs.volley)
    implementation(libs.androidx.material3)
    implementation(libs.androidx.compose.material.icons.extended)
    implementation(libs.androidx.ui)
    implementation(libs.androidx.navigation.runtime.ktx)
    testImplementation(libs.junit)
    androidTestImplementation(libs.androidx.junit)
    androidTestImplementation(libs.androidx.espresso.core)
    androidTestImplementation(platform(libs.androidx.compose.bom))
    androidTestImplementation(libs.androidx.compose.ui.test.junit4)
    debugImplementation(libs.androidx.compose.ui.tooling)
    debugImplementation(libs.androidx.compose.ui.test.manifest)

    implementation(libs.ktor.client.core)
    implementation(libs.ktor.client.cio)
    implementation(libs.ktor.client.content.negotiation)
    implementation(libs.ktor.serialization.kotlinx.json)
    implementation(libs.ktor.client.logging)
    implementation(libs.ktor.client.resources)
    implementation(libs.ktor.client.auth)

    implementation(libs.coil.compose)

    implementation(libs.androidx.compose.material.icons.core)

    implementation(libs.androidx.navigation.compose)

    implementation(libs.androidx.browser)
}

val copyOpenApiGenerated by tasks.registering(Copy::class) {
    dependsOn("openApiGenerate")
    from(layout.buildDirectory.dir("generate-resources/openapi/src/main/kotlin"))
    into("src/main/java")
}

tasks.withType<KotlinCompile>().configureEach {
    dependsOn(copyOpenApiGenerated)
}
